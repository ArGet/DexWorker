apply plugin: 'com.android.application'

ext {
    PATH_DEX = "./sample/src/main/assets/parser.dex"
}

def generateMD5( File file ) {
    def digest = java.security.MessageDigest.getInstance("MD5")
    file.eachByte( 4096 ) { buffer, length ->
        digest.update( buffer, 0, length )
    }
    new BigInteger(1, digest.digest()).toString(16).padLeft(32, '0')
}

android {
    compileSdkVersion rootProject.ext.sdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "ua.com.arget.dex_worker"
        minSdkVersion 19
        targetSdkVersion rootProject.ext.sdkVersion
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "MD5_DEX_ASSETS", "\"${generateMD5(new File(PATH_DEX))}\""
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation project(path: ':dex-worker')
}
